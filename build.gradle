buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.amkay:gradle-gitflow:0.2.0'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2'
    }
}

apply plugin: 'idea'
apply plugin: 'com.github.amkay.gitflow'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'
apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'maven-publish'

group = 'com.github.moaxcp'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacocoTestReport {
    dependsOn test
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':jacocoTestReport') || graph.hasTask(':sonarqube')) {
        compileGroovy.groovyOptions.optimizationOptions.all = false
    }
}

codenarc {
    ignoreFailures = true
    configFile = file 'config/codenarc/codenarc.groovy'
}

repositories {
    jcenter()
}

configurations {
    codnarc {
        extendsFrom compile
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}

task groovyDocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'groovydoc'
}

publishing {
    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }

    publications {
        graphDsl(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact groovyDocJar
            pom.withXml {
                asNode().appendNode('description', 'A groovy dsl for creating and traversing graphs.')
            }
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}