plugins {
    id 'io.codearte.nexus-staging' version '0.11.0'
    id 'org.ajoberstar.grgit' version '2.2.0'
    id 'org.ajoberstar.reckon' version '0.6.0'
    id 'org.ajoberstar.git-publish' version '0.4.1'
}

apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.moaxcp'
description = 'A groovy dsl for creating and traversing graphs.'

reckon {
    normal = scopeFromProp()
    preRelease = snapshotFromProp()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
ext.groovyVersion = '2.4.15'

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
    }
}

jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.88
            }
        }
    }
}

test {
    systemProperty 'com.athaydes.spockframework.report.hideEmptyBlocks', 'true'
    systemProperty 'com.athaydes.spockframework.report.showCodeBlocks', 'true'
    systemProperty 'com.athaydes.spockframework.report.projectVersion', version.toString()
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':jacocoTestReport') || graph.hasTask(':jacocoTestCoverageVerification')) {
        compileGroovy.groovyOptions.optimizationOptions.all = false
        logger.warn 'all groovy optimizations are turned off for jacoco'
    }
}

codenarc {
    configFile = file 'config/codenarc/codenarc.groovy'
    maxPriority1Violations = 0
    maxPriority2Violations = 2
    maxPriority3Violations = 1
    ignoreFailures = true
}

groovydoc {
    link 'https://docs.oracle.com/javase/8/docs/api/', 'java', 'javax', 'org'
    link "http://docs.groovy-lang.org/docs/groovy-$groovyVersion/html/api/", 'groovy', 'org'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}

task groovyDocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'groovydoc'
}

task javaDocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task createReadMeImages(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'groovy.ui.GroovyMain'
    args 'MakeImages.groovy'
}

artifacts {
    archives jar
    archives sourceJar
    archives groovyDocJar
    archives javaDocJar
}

signing {
    required = true
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }

            def publishUrl = ''
            if(version =~ 'SNAPSHOT$') {
                publishUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                publishUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }

            repository(url: publishUrl) {
                authentication(userName: System.getProperty('nexus.username'), password: System.getProperty('nexus.password'))
            }

            pom.project {
                name project.name
                description project.description
                packaging 'jar'
                url 'https://github.com/moaxcp/graph-dsl'
                scm {
                    connection 'scm:git:git://github.com/moaxcp/graph-dsl.git'
                    developerConnection 'scm:git:git@github.com:moaxcp/graph-dsl.git'
                    url 'https://github.com/moaxcp/graph-dsl'
                }
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = System.getProperty('nexus.username')
    password = System.getProperty('nexus.password')
    numberOfRetries = 10
    delayBetweenRetriesInMillis = 10000
}

gitPublish {
    repoUri = "https://github.com/moaxcp/graph-dsl.git"
    branch = 'gh-pages'
    contents {
        from 'build/docs'
        from 'build/reports'
    }
    commitMessage = 'Publishing from gradle.'
}

repositories {
    jcenter()
}

configurations {
    codnarc {
        extendsFrom compile
    }
}

dependencies {
    compile "org.codehaus.groovy:groovy:$groovyVersion"

    testCompile( 'com.athaydes:spock-reports:1.6.0' ) {
        transitive = false
    }
    testCompile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'org.slf4j:slf4j-simple:1.7.25'

    testCompile ('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module:'groovy-all'
    }
    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile 'cglib:cglib-nodep:3.2.6'

    testCompile 'org.hamcrest:hamcrest-all:1.3'
}