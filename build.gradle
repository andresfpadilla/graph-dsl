import static com.github.amkay.gradle.gitflow.version.VersionType.*

buildscript {
    dependencies {
        classpath 'com.github.amkay:gradle-gitflow:0.2.0'
    }
}

plugins {
    id 'net.ossindex.audit' version '0.1.1'
    id 'io.codearte.nexus-staging' version '0.8.0'
    id "org.standardout.versioneye" version "1.4.0"
}

apply plugin: 'idea'
apply plugin: 'com.github.amkay.gitflow'
apply plugin: 'jacoco'
apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.moaxcp'
description = 'A groovy dsl for creating and traversing graphs.'

sourceCompatibility = 1.8
targetCompatibility = 1.8
ext.groovyVersion = '2.4.12'

versioneye {
    includePlugins = true
    exclude configurations.findAll { !it.canBeResolved }*.name as String[]
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            limit {
                minimum = 0.83
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.88
            }
        }

        rule {
            element = 'CLASS'
            includes = ['graph.Graph']
            limit {
                minimum = 0.96
            }
            limit {
                counter = 'BRANCH'
                minimum = 1.0
            }
        }

        rule {
            element = 'CLASS'
            includes = ['graph.type.undirected.GraphEdgeSpec']
            limit {
                minimum = 0.93
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.92
            }
        }

        rule {
            element = 'CLASS'
            includes = ['graph.type.undirected.GraphVertexSpec']
            limit {
                minimum = 0.95
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.96
            }
        }
    }
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':jacocoTestReport') || graph.hasTask(':jacocoTestCoverageVerification')) {
        compileGroovy.groovyOptions.optimizationOptions.all = false
        logger.warn 'all groovy optimizations are turned off for jacoco'
    }
}

codenarc {
    configFile = file 'config/codenarc/codenarc.groovy'
    maxPriority1Violations = 0
    maxPriority2Violations = 2
    maxPriority3Violations = 1
    ignoreFailures = true
}

repositories {
    jcenter()
}

configurations {
    codnarc {
        extendsFrom compile
    }
}

groovydoc {
    link 'https://docs.oracle.com/javase/8/docs/api/', 'java', 'javax', 'org'
    link "http://docs.groovy-lang.org/docs/groovy-$groovyVersion/html/api/", 'groovy', 'org'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}

task groovyDocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'groovydoc'
}

task javaDocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives groovyDocJar
    archives javaDocJar
}

signing {
    required = true
    sign configurations.archives
}

if(!project.hasProperty('releaseType')) {
    ext.releaseType = 'RELEASE'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }

            def publishUrl = ''
            if(releaseType == 'RELEASE') {
                publishUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            } else if(releaseType == 'SNAPSHOT') {
                publishUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            }

            repository(url: publishUrl) {
                authentication(userName: System.getProperty('nexus.username'), password: System.getProperty('nexus.password'))
            }

            pom.project {
                name project.name
                description project.description
                packaging 'jar'
                url 'https://github.com/moaxcp/graph-dsl'
                scm {
                    connection 'scm:git:git://github.com/moaxcp/graph-dsl.git'
                    developerConnection 'scm:git:git@github.com:moaxcp/graph-dsl.git'
                    url 'https://github.com/moaxcp/graph-dsl'
                }
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = System.getProperty('nexus.username')
    password = System.getProperty('nexus.password')
    numberOfRetries = 10
    delayBetweenRetriesInMillis = 10000
}

dependencies {
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    testCompile( 'com.athaydes:spock-reports:1.3.2' ) {
        transitive = false
    }
    testCompile 'org.slf4j:slf4j-api:1.7.13'
    testCompile 'org.slf4j:slf4j-simple:1.7.13'
    testCompile ('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module:'groovy-all'
    }
    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
}